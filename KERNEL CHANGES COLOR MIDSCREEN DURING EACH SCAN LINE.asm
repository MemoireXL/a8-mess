;KERNEL EXAMPLE - BY DAN PINAL
.include "atari.inc"
.CODE
;    KERNEL CHANGES COLOR MIDSCREEN DURING EACH SCAN LINE
PDATA=$6000
MISSL0    = PDATA+$300
PLAYR0    = MISSL0+$100
PLAYR1    = PLAYR0+$100
PLAYR2    = PLAYR1+$100
PLAYR3    = PLAYR2+$100
STICKO    =$278
COLBAK    =$D01A
;ZERO PAGE EQUATES

VTEMPO=$F0
VTEMPI=$F1
POTMPO=$F8
POTMPI=$F9


.org    $4000
.proc Main
INIT:
   LDA #<DLIST
   STA SDLSTL
   LDA #>DLIST
   STA SDLSTL+1
   LDA #<KERNEL   ; SET DISPLAY LIST INTERRUPT
   STA VDSLST
   LDA #>KERNEL
   STA VDSLST+1
   LDY #<VBLANK   ; SET VERTICAL BLANK
   LDX #>VBLANK
   LDA #$07    ;DEFFERRED
   JSR SETVBV

   LDA #>PDATA ; INIT PM GRAPHICS
   STA PMBASE
   LDA #$3E
   STA SDMCTL

   LDA #03
   STA GRACTL

   LDA #>MISSL0 ;   CLEAR PLAYER/MISSILE RAM

   STA POTMPI
   LDA #00
   STA POTMPO
   TAY
   LDX #$05    ;5 PAGES
CLEARP:
   STA (POTMPO),Y
   INY

   BNE CLEARP
   INC POTMPI
   DEX
   BNE CLEARP
   LDA #$0E   ; WHITE
   STA PCOLR0
   LDA #$80
   STA PLAYRH
   STA PLAYRV
;ENABLE    INTERRUPTS
   LDA #$C0

   STA NMIEN
HERE:
   JMP HERE

VBLANK:
   LDA #$C0
   STA NMIEN    ;'REENABLE DLI (FOR REV. A O.S.)
   LDX STICKO
   CLC
   LDA PLAYRH
   ADC HOFF,X

   CMP #$30
;CHECK IF PAST RIGHT OR LEFF EDGE
   BCC NEWV
   CMP #$D0

   BCS NEWV
   STA PLAYRH
   STA HPOSP0 ; TELL ANTIC
NEWV:
   CLC

   LDA PLAYRV

   ADC VOFF,X
   ;CHECK IF PAST TOP OR BOTTOM

   CMP #$22
   BCC XVBLANK
   CMP #$D0
   BCS XVBLANK

   STA PLAYRV

   STA VTEMPO
   LDA #>PLAYR0
   STA VTEMPI
   LDY #$13  ;  20 ELEMENTS
PDRAW:
   LDA IMAGE,y
   STA (VTEMPO),Y

   DEY
   BPL PDRAW
XVBLANK:
   JMP XITVBV
;
KERNEL:

   PHA   ; SAVE REGISTERS
   TXA
   PHA
   TYA
   PHA

   LDY #$70
DALOOP:

   STA WSYNC   ; WAIT TILL BEAM OFFSCREEN

   LDA #$00    ;BLACK

   LDX #$06    ;DELAY TIMER
UNTIL:
   CPY VCOUNT    ;STILL ON SCREEN?
   BCC XKERNEL   ; LEAVE IF TOO LOW
   STA COLBAK    ;TURN BACKGROND TO BLACK
HANGON:
   DEX    ;COUNTDOWN FROM 6 TO 0
   BNE HANGON    ;BEAM MID-LINE YET
   LDX #$44    ;PINK
   STX COLBAK   ; TURN SCREEN PINK
   JMP DALOOP    ;GO GET READY FOR NEXT LINE
XKERNEL:

   LDA #$00    ;TURN SCREEN BLACK
   STA COLBAK

   PLA    ;RESTORE THE REGISTERS & LEAVE
   TAY
   PLA
   TAX
   PLA
   RTI

IMAGE:  
	.byte $00,$00,$10,$10,$10,$10,$38,$38
	.byte $38,$38,$7C,$7C,$7C,$FE,$FE,$54
    .byte $54,$00,$00
HOFF:
    .byte $00,$00,$00,$00,$00,$02,$02,$02
	.byte $00,$FE,$FE,$FE,$00,$00,$00,$00
VOFF:
    .byte $00,$00,$00,$00,$00,$02,$FE,$00
	.byte $00,$02,$FE,$00,$00,$02,$FE,$00
COLOR:
    .byte $44,$76,$B8,$3A
DLIST:    
	.byte $80,$41
	.byte <DLIST,>DLIST
PLAYRH:
	.byte $0
PLAYRV:
	.byte $0
	
END:	
.endproc
		
.SEGMENT "EXEHDR"
.word	$FFFF
.word	Main
.word	Main::END - 1

.segment "AUTOSTRT"
.word	RUNAD			; defined in atari.h
.word	RUNAD+1
.word	Main
